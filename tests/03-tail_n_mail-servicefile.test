#!perl

## Test service file related code for tail_n_mail

use 5.006;
use strict;
use warnings;
use autodie;
use Data::Dumper;
use lib 't','.';
use Test::More tests => 8;

my $datadir = '/tmp/tail_n_mail-testdb';
my $pidfile = "$datadir/postmaster.pid";
my $logfile = 'test.logfile';
my $test_service_conf = "$datadir/test.service.conf";
my $test_pgpass = "$datadir/test.pgpass";

sub cleanup {

    my $force = shift || 0;

    if (-e $pidfile && ! $ENV{TNM_NOSHUTDOWN}) {
        open my $fh, '<', $pidfile;
        <$fh> =~ /([0-9]+)/ or die "Could not find PID inside $pidfile!\n";
        my $pid = $1;
        close $fh;
        eval { kill (0, $pid) && kill ('KILL', $pid) && sleep 1 };
    }

    return if $ENV{TNM_NOCLEANUP} and ! $force;

    if (-e $datadir) {
        system "rm -fr $datadir";
    }

    if (-e $logfile) {
        unlink $logfile;
    }

    return;
}

END {
    cleanup();
}

sub run {

	my $arg = shift || '';

	my $COM = "perl tail_n_mail --dryrun --no-tailnmailrc --service_conf_file=$test_service_conf --pgpass_file=$test_pgpass $arg";

	my $result = '';
	eval {
		$result = qx{$COM 2>&1};
	};
    chomp $result;
	return $@ ? "OOPS: $@\n" : $result;
}

my $configfile = 'tests/config/testconfig-tail_n_mail.servicefile --offset 0';

## We need to fire up a Postgres instance so we can connect to it via a service file
## It also needs some logs

## initdb - not available everywhere, but let's assume it is for testing purposes
## socket in /tmp/tnm_testdb
## Stop the old one if it exists
cleanup('force');

my $COM = "initdb --pgdata $datadir --data-checksums 2>&1";
my $result = qx{ $COM };
$result =~ /Success/ or BAIL_OUT;
pass (' Tail_n_mail servicefile test created database via initdb call');

## Configure the database and start it up
my $conf = "$datadir/postgresql.conf";
my $socketdir = "$datadir/socket";
mkdir $socketdir;
open my $fh, '>>', $conf;
print {$fh} "
port = 5555
unix_socket_directories = '$socketdir'
logging_collector = on
log_directory = 'pg_log'
log_filename = 'postgresql-%Y-%m-%d.log'
log_statement = all
listen_addresses = ''
";
close $fh;
system "pg_ctl -D $datadir -l $logfile start >/dev/null";

my $t = q{ Tail_n_mail servicefile test fails when specified service.conf does not exist};
unlink $test_service_conf if -e $test_service_conf;
my $info = run($configfile);
is ($info, "Specified service.conf file does not exist: $test_service_conf", $t );
open $fh, '>', $test_service_conf;
print {$fh} "[foobar1]\nhost=$socketdir\nport=5555\ndbname=postgres\n\n";
print {$fh} "[foobar2]\nhost=$socketdir\nport=5555\ndbname=postgres\n\n";
close $fh;
chmod 0600, $test_service_conf;

$t = q{ Tail_n_mail servicefile test fails when specified pgpass file does not exist};
unlink $test_pgpass if -e $test_pgpass;
$info = run($configfile);
is ($info, "Specified pgpass file does not exist: $test_pgpass", $t );
open $fh, '>', $test_pgpass;
print {$fh} "blah:blah\n";
close $fh;
chmod 0600, $test_pgpass;

$t = q{ Tail_n_mail servicefile test works when no matching lines in the log file};
$info = run($configfile);
is ($info, '', $t);

$t = q{ Tail_n_mail servicefile test works when no matching lines and --mailzero, email is sent};
$info = run("$configfile --mailzero");
like ($info, qr/Unique items: 0/, $t);

## Create some errors
system "PGSERVICEFILE=$test_service_conf psql service=foobar1 -c 'SELECT 1/0' 2>/dev/null";

$t = q{ Tail_n_mail servicefile test works when matching lines and --mailzero, email is sent};
$info = run($configfile);
like ($info, qr/Unique items: 1\b/, $t);

## Create a lot more errors
my $numerrors = 22;
for my $number (1 .. $numerrors-1) {
    system "PGSERVICEFILE=$test_service_conf psql service=foobar1 -c 'GARBLE $number' 2>/dev/null";
}

##
$info = run($configfile);
##

$t = q{ Tail_n_mail servicefile test finds correct number of unique errors};
my $count = $info =~ /(Unique items: [0-9]+)/ ? $1 : '';
is ($count, "Unique items: $numerrors", $t);

$t = q{ Tail_n_mail servicefile test finds correct number of unique errors with multiple pg_read_file chunks};
$info = run("$configfile --pg_read_file_limit=200");
$count = $info =~ /(Unique items: [0-9]+)/ ? $1 : '';
is ($count, "Unique items: $numerrors", $t);

exit;

